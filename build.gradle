plugins {
    id 'idea'
    id 'java'
    id 'java-library'
    // Publishing maven artifacts
    id 'maven-publish'
    // ProtoClass Generation
    id 'com.google.protobuf' version '0.8.17'
    // To build Uber/Fat Jar
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group 'io.virtuelabs.poc'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    api 'io.grpc:grpc-protobuf:1.45.0'
    api 'io.grpc:grpc-stub:1.45.0'
    api 'com.google.protobuf:protobuf-java:3.19.4'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
}

sourceSets {
    main {
        java {
            srcDirs './build/generated/source/proto/main/grpc'
            srcDirs './build/generated/source/proto/main/java'
        }
    }
}

publishing {

    repositories {
        /* TODO: Enable publishing to Github
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/reddy-s/natscontracts"
            credentials {
                username = System.getenv("USER")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
        */

        // Publishing to .m2
        mavenLocal()
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.19.2"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.45.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}